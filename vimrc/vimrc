" vim:
"
" === --- General Rules ---------------------------------------------------- ===
"
" Rule 1. Config must be simple. Simplify it as much as possible.
" Rule 2. No fold. Fold is so hard for my little brain.
"

"
" === --- Panel Operations ------------------------------------------------- ===
"
" Ctrl-w v   -> open vertical split
"
" Ctrl-w c   -> close the current panel
" Ctrl-w o   -> close others
"
" Ctrl-w w   -> cycle to next window
" Ctrl-w p   -> go to previous (last) window
" Ctrl-w t   -> go to top-left window
" Ctrl-w b   -> go to bottom-right window
"
" Ctrl-w +   -> taller
" Ctrl-w -   -> shorter
" Ctrl-w >   -> wider
" Ctrl-w <   -> narrower

"
" === --- Comments, Formating, and Pattern --------------------------------- ===
"
" - Comments is a comma separated list of string parts to start comments. A
"   part has format {flags}:{literal text}
"
" - Formatoptions: a string that contains any letters below to control Vim fmt
"
"   t  Auto-wrap text using textwidth
"   c  Auto-wrap comments using textwidth, inserting the current comment
"      leader automatically.
"   r  Automatically insert the current comment leader after hitting
"      <enter> in Insert mode.
"   o  Automatically insert the current comment leader after hitting 'o' or
"      'O' in Normal mode.
"   q  Allow formatting of comments with "gq".
"      Note that formatting will not change blank lines or lines containing
"      only the comment leader.
"   n  When formatting text, recognize numbered lists.  This actually uses
"      the 'formatlistpat' option, thus any kind of list can be used. Note that
"      'autoindent' must be set too.  Doesn't work well together with "2".
"   2  When formatting text, use the indent of the second line of a paragraph
"      for the rest of the paragraph, instead of the indent of the first
"      line.  This supports paragraphs in which the first line has a
"      different indent than the rest.  Note that 'autoindent' must be set.
"
" - Formatlistpat or flp:  A pattern that is used to recognize a list header.
"   This is used for the "n" flag in 'formatoptions'.
"
" - Pattern: with `magic` on,
"
"       $      matches end-of-line
"       .      matches any character
"       *      any number of the previous atom
"       ~      latest substitute string
"       \(\)   grouping into an atom
"       \|     separating alternatives
"       \a     alphabetic character
"       \\     literal backslash
"       \.     literal dot
"       {      literal '{'
"       a      literal 'a'
"
"   See :h pattern.
"
"   For example, in search cmd, /a\(b\|c\) will search ab and ac.
"
"   For option value, the number of backslash is doubled. For example the flp
"   used in this vimrc. see :h option-backslash.
"

"
" === --- General Settings ------------------------------------------------- ===
"
syntax on               " enable syntax highlighting

set nocompatible        " make Vim behave in a more useful way
set mouse=a             " enable mouse in all modes (normal, visual, etc)
set colorcolumn=80,100  " screen columns that are highlighted with ColorColumn
set backspace=2         " make backspace work like most other programs
set splitright          " if off, vsplit to left
set shiftwidth=2        " number of spaces to use for each step of (auto)indent
set tabstop=2           " number of spaces that a <tab> in the file counts for
set expandtab           " in insert mode: use spaces to insert a <tab>
set softtabstop=2       " number of spaces that a <tab> counts for while editing
set cindent             " indent for line according the C indenting rules
set noignorecase        " (no) ignore case in a pattern
set noinfercase         " (no) case sensitive for auto-complete
set showcmd             " show selecting area size in visual mode, cmd in normal
set nu                  " line number
set cursorline          " highlight the cursor line with CursorLine
set ruler               " show the line and column number of the cursor pos
set vb                  " use a visual bell instead of beeping
set modeline            " enable the 'modeline' option when opening a new file
set modelines=5         " must be set to enable modeline
set incsearch           " search pattern while typing in search.
set encoding=utf8       " character encoding
set nofoldenable        " disable fold in general.
set nospell             " disable spell. spelllang=en_us
set nowrapscan          " disable searches wrapping around the end of the file
set list                " this and line below show the tab as visible symbol
set listchars=tab:>-

set wildmenu            " possible matches are shown just above the command line
set wildmode=longest:full " emacs-style filename matching
set completeopt=longest,menuone " completion mode in insert mode

filetype plugin indent on
filetype plugin on

" === --- Disable netrw
"
" disable netrw (Vimâ€™s built-in network and file explorer plugin) which writes
" a .netrwhist file in ~/.vim when open dir.
let g:loaded_netrw       = 1
let g:loaded_netrwPlugin = 1

"
" === --- Plugins ---------------------------------------------------------- ===
"
call plug#begin('~/.vim/plugged')

" === --- Airline
"
Plug 'vim-airline/vim-airline', {'commit': 'f86f1e8' }  " 2017-06
let g:airline_section_b = '%{strftime("%c")}'
set laststatus=2  " Enable the nice status bar show filename, mode, file type...

" === --- Fzf
"
" search FZF_DEFAULT_COMMAND in bash_profile
Plug 'junegunn/fzf', {'dir': '~/.fzf', 'do': './install --bin', 'commit': '9cb7a36'} " 2022-01
Plug 'junegunn/fzf.vim', {'commit': '70541d2'} " 2022-01

" === --- Dracula (Not Used Now)
"
" adjust: disable italic fonts to avoid the background highlight bugs.
" see https://github.com/dracula/vim/issues/81
"
" Plug 'dracula/vim', {'commit': 'b64b22a'} " 2020-08
" let g:dracula_italic = 0

" === --- Nord
"
Plug 'arcticicestudio/nord-vim', {'commit': 'bc0f057'} " 2022-09

call plug#end()

"
" === --- Color Settings --------------------------------------------------- ===
"

" Colorscheme
"
set background=dark " background must be set before colorscheme
colorscheme nord

" Color Adjustments
"
" Define color for current line number. `set cursorline` must be set.
hi CursorLineNR ctermfg=Cyan

" Define a special group for some languages, used in c, etc.
hi MyCKeyword ctermbg=DarkCyan ctermfg=Black

" Change comment to darg green. makes it visible in terminal (usually is blue).
"hi Comment ctermfg=DarkMagenta
hi Comment ctermfg=LightGrey

" Make the visual selection more visible. 166 is orange.
hi visual ctermbg=166

" Make the pop up menu more visible. 166 is orange.
hi PmenuSel ctermfg=White ctermbg=166

"
" === --- Leaders ---------------------------------------------------------- ===
"
let mapleader = ","
set timeoutlen=500

" Refresh or save the file content
"
nmap <silent> <leader>r :e! <CR>

" Navigation for buffers
"
nmap <silent> <leader>b :bn! <CR>
nmap <silent> <leader>B :bp! <CR>
nmap <silent> <leader>t :tabn <CR>
nmap <silent> <leader>T :tabp <CR>
nmap <silent> <leader>1 :tabn 1<CR>
nmap <silent> <leader>2 :tabn 2<CR>
nmap <silent> <leader>3 :tabn 3<CR>
nmap <silent> <leader>4 :tabn 4<CR>

" Some switches for spell, paste, etc
"
nmap <silent> <leader>p :set paste<CR>
nmap <silent> <leader>np :set nopaste<CR>
nmap <silent> <leader>s :set spell<CR>
nmap <silent> <leader>ns :set nospell<CR>

" List the files to open, such as recent files, pending files in git
"
nmap <silent> <leader>lb :call fns#LoadRecentFiles()<CR>
nmap <silent> <leader>ll :call fns#LoadPendingFiles()<CR>

" FZF
"
" check FZF_DEFAULT_COMMAND env var for the command to use.
"
nmap <silent> <leader>z :call fzf#run({
        \ 'source': $FZF_DEFAULT_COMMAND,
        \ 'sink': 'e',
        \ 'options': '-i',
        \ 'down': '40%'})<cr>


" Open files related
"
" open in current working dir
" open in current working dir but in a new tab
"
nmap <silent> <leader>e :e %:p:h/
nmap <silent> <leader>t :tabnew %:p:h/

" Put `` and '' around a word
"
nmap <silent> <leader>` bi`<esc>wea`<esc>
nmap <silent> <leader>' bi'<esc>wea'<esc>

" Insert a 80-char line
"
" useful in comments and ft=help mode
"
nmap <silent> <leader>- i-<esc>79.
nmap <silent> <leader>= i=<esc>79.

"
" === --- Other Mappings --------------------------------------------------- ===
"

" Kill the current buffer without losing split
"
nmap <silent> <leader>d :bp \| bd #<CR>

" Map tab to auto complete
"
imap <tab> <c-x><c-p>

"
" === --- General File Related --------------------------------------------- ===
"

" Delete tailing spaces for all lines (all files). The env var can turn it
" off.
if empty($DOTFILES_VIM_NOT_DEL_EMPTY_LINESEND)
  autocmd BufWritePre * :call fns#DelEmptyLinesEnd()
endif

" Auto detect and load file change
"
" Ask vim to check file timestamp (checktime). Once detected new change, due
" to `autoread`, the content will be loaded.
autocmd CursorHold * checktime
set autoread

"
" === --- CC --------------------------------------------------------------- ===
"

autocmd FileType cpp :setlocal shiftwidth=2 tabstop=2 softtabstop=2

" Highlight field (-> or .) for c and adjust the colors for it
autocmd FileType cpp :syn match cField /\(->\|\.\)\<\k\+\>/
autocmd FileType cpp :hi cField ctermfg=Green

" Color for String.
autocmd FileType cpp :hi String ctermfg=Red

" Highlight function for c, check \ze in pattern
autocmd FileType cpp :syn match cFunction "\<\k\+\ze("
autocmd FileType cpp :hi link cFunction Function

" Highlight the entire type with type name for struct/union/enum/class
" (instead of just the keywords)
autocmd FileType cpp :syn clear cStructure
autocmd FileType cpp :syn clear cppStructure
autocmd FileType cpp :syn match cStructureType /\(struct\|union\|class\|enum\|namespace\) \<\k\+\>/
autocmd FileType cpp :hi cStructureType ctermfg=Yellow

" Highlight namespace as well (cppStructure is cleared above). Supports
" annoymous namespace as well.
autocmd FileType cpp :syn match cNamespaceType /\(namespace\) \(\<\k\+\>\|\)/
autocmd FileType cpp :hi cNamespaceType ctermfg=Magenta

" Highlight namespace prefix
autocmd FileType cpp :syn match cNamespaceType /\<\k\+\>::/

" Nested lists inside comments
autocmd FileType cpp :setlocal formatoptions+=n flp=^\\s*\\(\\d\\+\\.\\\|-\\)\\s

"
" === --- C ---------------------------------------------------------------- ===
"
" Make header files as c not cpp.
" Set shift and tabstop per linux kernel style.
"
autocmd FileType c :setlocal shiftwidth=8 tabstop=8 softtabstop=8
autocmd FileType c :setlocal nofoldenable
autocmd BufRead,BufNewFile *.h setlocal filetype=c

" Customize syntax for c
" - see 'help usr_44.tx'
" - see colors in https://en.wikipedia.org/wiki/ANSI_escape_code#Colors
"
" Define some keywords for storage, e.g., _mut_, _out_,
autocmd FileType c :match cStorageClass /\<_MUT_\|_OUT_\|_INOUT_\|_MOVED_IN_\|_NULLABLE_\>/

" Add more int types and error_t as cType
autocmd FileType c :syn keyword	cType	i64_t u64_t i32_t u32_t f32_t f64_t byte
autocmd FileType c :syn keyword	cType	i64 u64 i32 u32 u8 f32 f64
autocmd FileType c :syn keyword	cType	vec_t error_t sds_t

" Highlight function for c, check \ze in pattern
autocmd FileType c :syn match cFunction "\<\k\+\ze("
autocmd FileType c :hi link cFunction Function

" Highlight field (-> or .) for c and adjust the colors for it and String.
autocmd FileType c :syn match cField /\(->\|\.\)\<\k\+\>/
autocmd FileType c :hi cField ctermfg=Green
autocmd FileType c :hi String ctermfg=Red

" Highlight the entire type with type name for struct/union/enum (instead of
" just the keywords)
autocmd FileType c :syn clear cStructure
autocmd FileType c :syn match cStructureType /\(struct\|union\|enum\) \<\k\+\>/
autocmd FileType c :hi cStructureType ctermfg=Yellow

" Disable incorrect curly warning for compound literal.
let g:c_no_curly_error=1

" Make list in comments formatting easier.
autocmd FileType c :setlocal formatoptions+=n flp=^\\s*\\(\\d\\+\\.\\\|-\\)\\s

"
" === --- Others ----------------------------------------------------------- ===
"
source ~/Workspace/y/vimrc/vimrc.programming_language.vim
